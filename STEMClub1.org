# # -*- mode: org -*- coding: utf-8 -*-
#+TITLE: 
#+AUTHOR:
#+EMAIL: 
#+DATE: 
#+LANGUAGE:  en
# #+INFOJS_OPT: view:showall ltoc:t mouse:underline
#+HTML_HEAD: <link rel="stylesheet" href="./tufte.css" type="text/css">
#+HTML_HEAD: <link rel="stylesheet" href="./ox-tufte.css" type="text/css">
#+HTML_HEAD_EXTRA: <style>
#+HTML_HEAD_EXTRA: article > div.org-src-container {
#+HTML_HEAD_EXTRA:     width: var(--ox-tufte-content-width);
#+HTML_HEAD_EXTRA:     max-width: var(--ox-tufte-content-width);
#+HTML_HEAD_EXTRA:     clear: none;
#+HTML_HEAD_EXTRA: }
#+HTML_HEAD_EXTRA: article > section .org-src-container {
#+HTML_HEAD_EXTRA:     width: var(--ox-tufte-src-code-width);
#+HTML_HEAD_EXTRA:     max-width: var(--ox-tufte-src-code-width);
#+HTML_HEAD_EXTRA:     clear: none;
#+HTML_HEAD_EXTRA: }
#+HTML_HEAD_EXTRA: div.org-src-container > pre { clear: none; }
#+HTML_HEAD_EXTRA: pre.example {clear: none; }
#+HTML_HEAD_EXTRA: </style>
#+INCLUDE: "./header.org" :minlevel 1
#+EXPORT_SELECT_TAGS: export
#+EXPORT_EXCLUDE_TAGS: noexport
#+EXPORT_FILE_NAME: STEMClub1.html
#+OPTIONS: H:15 num:nil toc:nil \n:nil @:t ::t |:t _:{} *:t ^:{} prop:nil
#+OPTIONS: tex:t
#+OPTIONS: html-postamble:nil
#+LATEX_CLASS: article
#+LATEX_CLASS_OPTIONS: [american]
# Setup tikz package for both LaTeX and HTML export:
#+LATEX_HEADER: \usepackqqqage{tikz}
#+LATEX_HEADER: \usepackage{commath}
#+LaTeX_HEADER: \usepackage{pgfplots}
#+LaTeX_HEADER: \usepackage{sansmath}
#+LaTeX_HEADER: \usepackage{mathtools}
#+PROPERTY: header-args:latex+ :packages '(("" "tikz"))
#
#+PROPERTY: header-args:latex+ :exports results :fit yes
#
#+STARTUP: showall
#+STARTUP: align
#+STARTUP: indent
#+STARTUP: shrink
# This makes MathJax/LaTeX appear in buffer (UTF-8)
#+STARTUP: entitiespretty
# #+STARTUP: logdrawer # This makes pictures appear in buffer
#+STARTUP: inlineimages
#+STARTUP: fnadjust
# #+OPTIONS: html-style:nil
#+html_head_extra: <style> .title { display: none; } </style>
#+html_head_extra: <style> caption.t-bottom { caption-side: bottom; } </style>

* STEM Club 1

**  Bibliography :noexport:
:RESOURCES:
- [[bibliography:~/Dropbox/org/biblio/ref.bib][Bibliography]]
:END:

** Not exported :noexport:

#+name: module
#+begin_src haskell :eval never :exports code :tangle ./src/stemclub1.hs :noweb yes
module STEMCLUB1 where
<<1e87308f-8ab6-43ce-87a9-1e3818c63753>>

<<af3e4f87-3492-4f87-8e19-6d43b5bcf035>>

<<9d7afc7e-4e0d-4bcd-aef7-1eb81a5310b0>>

<<929a7a61-6918-4f5f-9a89-502b66003760>>

<<dadbe877-e9c4-436e-843d-144005fef37d>>

<<94268e73-4fb6-465b-b810-fdd34c488c72>>

<<4d37a5ff-41e7-4efb-83ec-bdf53b60e009>>

<<4d38f6dd-987b-4709-bee2-8de927d0ce94>>

#+end_src

#+name: 2b702810-8387-4ce3-af0c-067fd15b4e33
#+begin_src haskell :session *myhaskell*
1 + 1
#+end_src

#+RESULTS: 2b702810-8387-4ce3-af0c-067fd15b4e33
: 2

#+name: 1e87308f-8ab6-43ce-87a9-1e3818c63753
#+begin_src haskell :eval never :exports code
import GHC.Real
#+end_src


* 

** Doing math with computers

*The birth of mathematics*:

#+begin_quote
Caveman #1 (looking at flock of birds): Many. \\
Caveman #2: *How* many?
#+end_quote

*Q*: Why do we do STEM? \\
*A*: Because somebody will pay us to do science, technology,
engineering, and especially mathematics for them.

*/Where/ is math?*[fn:1]

1. In our brains.
2. In math books and papers.
3. On classroom white/black boards.
4. Built into various mechanical devices
5. In digital computing devices as circuitry and software.

...but, yes, now more than ever #5. So what does it mean to have math
expressed in software.

*How do computers "compute" stuff?*[fn:2]

- Develop algorithms from math
- Encode algorithms in programming language
- Run code on device 

In other words, we're taking the math out of books, papers, off
blackboards, out of brains, reverse-engineering mechanical
devices---and packing it into digital computing devices, mainly in the
form of software, code. And with the digital computing devices count
about to exceed /three times/ the world's human population, that's a
big deal.

** Basic fractions on the computer

What we call *[[https://en.wikipedia.org/wiki/Fraction][fractions]]* in everyday life and are written in the form
$\frac{a}{b}$ are either meant to represent a *ratio*, i.e., $a : b$
and spoken $a$ to $b$ or $a$ is to $b$, or, more commonly, to imply $a
\div b$, i.e., $b$ divides (into) $a$. Although we normally call the
/outcome/ of division $a \div b$ the *quotient* (with a possible
remainder), a fraction as a whole is called a quotient as well

\begin{align*}
\text{quotient}\;\left\{ \quad \begin{array}{l}
\underline{1} & \leftarrow \text{dividend or numerator} \\[-5pt]
2 &  \leftarrow \text{divisor or denominator}
\end{array} \right.
\end{align*}

*[[https://en.wikipedia.org/wiki/Rational_number][Rational numbers]]* are numbers that are expressed in the fraction form
$\frac{a}{b}$ where both $a$ and $b$ are *integers* and $b \ne 0$. That
means, e.g., $\frac{\sqrt{2}}{2}$ is not a rational number, but is
still considered a fraction. And so we can call any rational number a
fraction, but not all fractions are rational numbers.[fn:3] The set of
rational numbers is denoted by $\mathbb{Q}$, which, again, stands for
/quotient/.



Let's start with something presumably simple like how to do
fractions---specifically, /adding, subtracting unlike denominator
fractions/---on the computer. We'll first dive into the world of
number theory and its concentration on the behavior of prime
numbers. So what is the working definition of a prime whole number?

@@html:<font color = "#4715b3">@@ ‚•§ A *prime number* is an integer
that can only be /divided evenly/ (producing no remainder) by $1$ or
itself.  @@html:</font>@@ \\
\\
The opposite of a prime number is a *composite number*, i.e., an
integer that is a /multiple/ of other numbers beside $1$ and the
number, let's call it $n$, itself. With composites there does exist a
number, call it $d$, between $1$ and $n$ that will evenly divide
it. There are many consequences of these two number opposites. One is
the fact that integers are made up of primes

@@html:<font color = "#4715b3">@@ ‚á≤ Theorem: *Fundamental Theorem of
Arithmetic* \\
Every positive integer $ùëõ \gt 1$ can be represented in exactly one way
as a product of prime powers.@@html:</font>@@


- *Q*: What is meant by prime /powers/, why not just primes? \\
- *A*: By "prime power" we simply mean a prime raised to a
  power. Consider these /composite numbers/ broken down into products
  of prime powers

\begin{align*}
4 &= 2^2 \\
8 &= 2^3 \\
12 &= 2^2 \cdot 3^1 \\
25 &= 5^2 \\
50 &= 2^1 \cdot 5^2 \\
72 &= 2^3 \cdot 3^2 \\
128 &= 2^7 \\
\ldots
\end{align*} 

Let's use Scheme's function ~expt~ to find $2^7$

#+name: 010e1d3f-41fa-4ad8-991a-4a6bf11715c4
#+begin_src scheme :session *littleschemer* :results verbatim :exports both
(expt 2 7)
#+end_src

#+RESULTS: 010e1d3f-41fa-4ad8-991a-4a6bf11715c4
: 128

This idea of multiplying unique sets of primes to get any whole number
is an almost mysterious thing in mathematics. The [[https://en.wikipedia.org/wiki/Peano_axioms][Peano Axioms]],
supposedly the most bedrock foundation of mathematics, says if we
start with zero we can get any of the natural counting numbers,
$\mathbb{N}$,[fn:4] by simply adding "one more" over and over.[fn:5]
But we can also "get" any $n \in \mathbb{N}$ by multiplying sets of
primes. Hence, we can almost say, prime numbers /necessitate/ (bring
into existence?) multiplication.

Let's jump way ahead just for a moment. [[https://gemini.google.com/app][Google Gemini]] produced this
Haskell code when the keywords /haskell prime factors of a number/
were entered into a Gemini search[fn:6]

#+name: af3e4f87-3492-4f87-8e19-6d43b5bcf035
#+begin_src haskell :eval never :exports code
primeFactors :: Integer -> [Integer] 
primeFactors n = go [2..] n
  where
    go primes num 
      | num == 1 = [] 
      | head primes * head primes > num = [num] 
      | num `mod` head primes == 0 = head primes : go primes (num `div` head primes) 
      | otherwise = go (tail primes) num
#+end_src

#+name: bb062a62-db47-419d-a3ad-c8ade99a8ec2
#+begin_src haskell :results verbatim :exports both
primeFactors 120
#+end_src

#+RESULTS: bb062a62-db47-419d-a3ad-c8ade99a8ec2
: [2,2,2,3,5]

Too much all at once. Better to break things down, like the approach
taken in [[https://www.collegepublications.co.uk/computing/?00004][/The
Haskell Road to Logic, Maths, and Programming/]] by Doets and van
Eijck. They start with the

*** Prime number test

Again, prime numbers: $2, 3, 5, 7, 11, 13,\ldots$, divisible only by
$1$ or the number itself.

‚á≤ Definition *Divides*

@@html:<font color = "#4715b3">@@ If $a, b \in \mathbb{Z}$, the
integers, we say that $a$ divides $b$, written $a \mid b$, if $a \cdot c =
b$ for some $c ‚àà \mathbb{Z}$. In this case, we say $a$ is a /divisor/
of $b$. We say that $a$ does not divide $b$, written $a \nmid b$, if
there is no $c ‚àà \mathbb{Z}$ such that $a \cdot c = b$.  @@html:</font>@@

Division is the inverse of multiplication, e.g., $\frac{6}{3} = x$
is identical to $x \cdot 3 = 6$ as we've seen in basic algebra. Defining
/divides/ in terms of multiplication by another whole number implies
division without a remainder. $2 \mid 6$ ($2$ /divides/ $6$) and $-3
\mid 15$ ($-3$ /divides/ $6$). All integers divide $0$, but only $0$
divides $0$. This is why division by $0$ is problematic. $q =
\frac{a}{0}$ would imply $q \cdot 0 = a$, but if $a$ is not zero, then
this is nonsense.

Now, let's turn *divides* into code

#+name: 9d7afc7e-4e0d-4bcd-aef7-1eb81a5310b0
#+begin_src haskell :eval never :exports code
divides d n = rem n d == 0
#+end_src

This is simple, but there's a lot going on for the beginner. We see
~=~, the /is defined as/ or /is by definition equal to/ symbol in
Haskell---which makes anything using it an *equation*, i.e., ~divides
d n = rem n d == 0~ is an equation. We also see on the right hand side
of ~=~ the double-equals symbol ~==~ for *identity*. Let's look into
exactly what an identity is in the math world.

‚á≤ Definition *Identity*

@@html:<font color = "#4715b3">@@ An *identity* is a type of
/equality/ relating one mathematical expression $A$ to another
mathematical expression $B$, such that $A$ and $B$ (which might
contain some variables) produce the same value for all values of the
variables within a certain domain of discourse.  @@html:</font>@@

Unfortunately, two symbols are used to indicate identity. For example

\begin{align*}
(a + b)^2 = a^2 + 2ab + b^2
\end{align*}

uses the $=$ equals sign to indicate /identical values/ are guaranteed
given any specific $a$ or $b$.[fn:7] Another identity would be

\begin{align*}
sin^{2}\theta + cos^{2}\theta = 1
\end{align*}

Again, we mean the left- and right-hand sides are identical
mathematically, as in, both sides have identical values, although
they're obviously different symbols.[fn:8] /Which means they are
interchangeable/. Which means when solving a trig problem, you can
substitute in one for the other. Keep track of the subtle difference
between /is defined as/ and /identical values/.

Having said all this, the Haskell operator ~==~ and its opposite ~/=~
are actually functions that take two /formal arguments/ and return a
"decision" of whether they are or are not identical. In our ~divides~
code we have ~rem~ which takes two arguments and returns only the
remainder of dividing the first argument by the second

#+name: 83946008-f006-4f83-8185-58adef363236
#+begin_src haskell :results verbatim :exports both
rem 8 4
#+end_src

#+RESULTS: 83946008-f006-4f83-8185-58adef363236
: 0

#+name: 12f5ec95-d089-4f43-a731-a532b5d35dcb
#+begin_src haskell :results verbatim :exports both
rem 9 5
#+end_src

#+RESULTS: 12f5ec95-d089-4f43-a731-a532b5d35dcb
: 4

i.e., ~rem~ throws away the quotient and keeps just the
remainder.

In Haskell, being a *typed language* means that every expression in
the program has a type---such as integer, real number, characters in
an alphabet, etc.---and these types are checked by the Haskell
compiler to make sure we're not doing something strange, e.g., adding
the number $5$ to the letter $z$, before the program is run. This type
checking is a fundamental part of Haskell's design and contributes
significantly to its safety, reliability, and expressiveness. The
Haskell REPL command ~:type~ (~:t~ for short) will give us back the
/type signature/ of ~rem~, i.e., what type(s) ~rem~ can take for input
arguments, how many, and what it will return as its value when
evaluated

#+name: 1a8f7e6a-1f68-47ff-bda8-829a677e0f02
#+begin_src haskell :results verbatim :exports both
:t rem
#+end_src

#+RESULTS: 1a8f7e6a-1f68-47ff-bda8-829a677e0f02
: rem :: Integral a => a -> a -> a

In this case ~a~ is a *unconstrained type variable* instead
of one specific type. This means ~rem~ can take different types for
input. What different types? The signature starts with ~Integral a
=>~, which means any type of the ~Integral~ class. Hold that
thought...

To the right of ~Integral =>~ we see the chain ~a -> a -> a~. This is
the actual breakdown of the function type-wise. The type of the
function's output is always the last symbol on the far right, while
the ~a -> a~ indicates two input arguments. Which makes sense---~rem~
takes two arguments and returns one answer.

Back to ~Integral~. Which is a *type class* declared internally in
Haskell. The ~Integral a =>~ prefix in the type signature of ~rem~
says ~a~ can be any of the individual types that have "registered"
with the type class ~Integral~. Let's unpack the ~Integral~ type class
with the Haskell REPL command ~:info~ (or just ~:i~)[fn:9]

#+name: 1219802e-34bc-4d13-ab8e-27e7119f07db
#+begin_src haskell :results verbatim :exports both
:info Integral
#+end_src

#+RESULTS: 1219802e-34bc-4d13-ab8e-27e7119f07db
#+begin_example
type Integral :: * -> Constraint
class (Real a, Enum a) => Integral a where
  quot :: a -> a -> a
  rem :: a -> a -> a
  div :: a -> a -> a
  mod :: a -> a -> a
  quotRem :: a -> a -> (a, a)
  divMod :: a -> a -> (a, a)
  toInteger :: a -> Integer
  {-# MINIMAL quotRem, toInteger #-}
  	-- Defined in ‚ÄòGHC.Real‚Äô
instance Integral Int -- Defined in ‚ÄòGHC.Real‚Äô
instance Integral Integer -- Defined in ‚ÄòGHC.Real‚Äô
instance Integral Word -- Defined in ‚ÄòGHC.Real‚Äô
#+end_example

We'll dive deeper into the whole type class story later, but for now
just know that ~Integral~ provides /generic/ whole-number division and
remainder functions, ~quot~, ~rem~, ~div~, ~mod~, ~quotRem~, ~divMod~,
and ~toInteger~, which are then customized by the specific types
~Int~, ~Integer~. This output tells us that any type that wants to
have the functions ~quot~, ~rem~, ..., ~toInteger~ needs to have a
registered /instance/ with type class ~Integral~. Which means the
types ~Int~, ~Integer~, and ~Word~ do indeed have their own specific
versions of the basic functions ~quot~, ~rem~,...

Now, back to ~divides d n = rem n d == 0~, where the left-hand side
~divides d n~ is really just a nice /is-defined-as/ wrapper for the
/identity/ ~rem n d == 0~, i.e., we have created a Haskell version of
$d \mid n$. Let's look at its type

#+name: 2c92d4df-1f1b-4ed1-a70a-d9245a97059b
#+begin_src haskell :results verbatim :exports both
:t divides
#+end_src

#+RESULTS: 2c92d4df-1f1b-4ed1-a70a-d9245a97059b
: divides :: Integral a => a -> a -> Bool

Haskell reports ~divides~ takes two arguments of type ~a~,
meaning, as we just discussed, they could be any of the three types
instance registration with ~Integral~, and gives back a value of type
~Boolean~. So what does this ~Boolean~ mean? A ~Boolean~ is a type of
/truth value/. While the set of natural numbers $\mathbb{N} =
\{0,1,2,\ldots\}$ is endless, the ~Boolean~ set is made up of just two
elements, $\{\top, \bot\}$ (logic symbols for /top/ or /truth/ and
/bottom/ or /falsity/) or in Haskell simply ~True~ and ~False~.

In the type signature for ~divides~ we see the two input arguments ~n~
and ~d~, but why does the code ~...rem n d == 0~ return as its value
either a ~True~ or ~False~. Because in the realm of Haskell,
/everything has a value/. While $sin^{2}\theta + cos^{2}\theta = 1$ is indeed an
identity, what is the /value/ of this identity? Here we enter the
realm of mathematical logic. While the value of the left-hand side,
$sin^{2}\theta + cos^{2}\theta$, becomes whatever $\theta$ we plug in, and the value of the
right-hand side, $1$, is just $1$, the /value of the identity as a
whole/ is either true or false. And so the Haskell identity operator
~(==)~ /returns the value ~True~ or ~False~

#+name: 7db9545a-cc55-450a-b3f8-e08b5b5c2cba
#+begin_src haskell :results verbatim :exports both
:t (==)
#+end_src

#+RESULTS: 7db9545a-cc55-450a-b3f8-e08b5b5c2cba
: (==) :: Eq a => a -> a -> Bool

Again, we see ~a~ as a type variable, meaning more than one type is
possible. And we see reference to a type class. This time the ~Eq a
=>~ indicates whatever actual types ~a~ is, those types must each be
registered with the type class ~Eq~. And what is ~Eq~?

#+name: a7b19c2d-8b81-4e9b-89d9-1f3a6ca9d35b
#+begin_src haskell :eval never :exports code
:i Eq
#+end_src

#+begin_example
type Eq :: * -> Constraint
class Eq a where
  (==) :: a -> a -> Bool
  (/=) :: a -> a -> Bool
  {-# MINIMAL (==) | (/=) #-}
  	-- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Integer -- Defined in ‚ÄòGHC.Num.Integer‚Äô
instance (Eq a, Eq b) => Eq (Either a b)
  -- Defined in ‚ÄòData.Either‚Äô
instance Eq a => Eq (Maybe a) -- Defined in ‚ÄòGHC.Maybe‚Äô
instance Eq a => Eq (Ratio a) -- Defined in ‚ÄòGHC.Real‚Äô
...
instance Eq Bool -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Char -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Double -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Float -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Int -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Ordering -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq a => Eq (Solo a)
  -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq Word -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
instance Eq a => Eq [a] -- Defined in ‚Äòghc-prim-0.9.1:GHC.Classes‚Äô
#+end_example

As the ellipsis (...) indicate, much was cut out for brevity. The idea
behind ~Eq~ is mathematical logic equality or *equivalence*[fn:10]

‚•§ *Side note* establishing equivalence \\

It may be obvious that $1$ is equivalent to $1$, and all the
~Integer~, ~Double~, etc. numbers are equal in value to themselves,
but what if we have created our own data type, say

#+name: 929a7a61-6918-4f5f-9a89-502b66003760
#+begin_src haskell :eval never :exports code
data Color = Red | Yellow | Blue | Green deriving (Show,Read)
#+end_src

Can we /add/ these self-made colors? Not really

#+name: 242f84f3-7270-4260-a8f3-26a8a926b562
#+begin_src haskell :eval never :exports code
Red + Yellow
#+end_src

#+begin_example
<interactive>:628:5: error:
    ‚Ä¢ No instance for (Num Color) arising from a use of ‚Äò+‚Äô
    ‚Ä¢ In the expression: Red + Yellow
#+end_example

...meaning our ~Color~ type does not have addition defined.[fn:11] The
error message indicates the type class ~Num~ doesn't have an
/instance/ of ~Color~ registered with it. Can we /compare/ colors for
order or equivalence? Not yet. But if we specifically create an
instance for ~Eq~ telling Haskell what is, e.g., equivalent

#+name: dadbe877-e9c4-436e-843d-144005fef37d
#+begin_src haskell :eval never :exports code
instance Eq Color where
  Red == Red = True      -- could also be (==) Red Red = True
  Yellow == Yellow = True
  Blue == Blue = True
  Green == Green = True
  _ == _ = False         -- by now anything else must be false
#+end_src

we now can test for equivalence

#+name: c8a4b3f9-ed5c-4157-8595-10c069927433
#+begin_src haskell :results verbatim :exports both
Blue == Blue
#+end_src

#+RESULTS: c8a4b3f9-ed5c-4157-8595-10c069927433
: True

#+name: b5e6cda1-5ae5-4bd9-a037-73d1629a17ed
#+begin_src haskell :results verbatim :exports both
Red == Blue
#+end_src

#+RESULTS: b5e6cda1-5ae5-4bd9-a037-73d1629a17ed
: False

Good. More Haskell lore as we go. So we have a Haskell code version of
math's $d$ /divides/ $n$. Let's move on.

*** A /least divisor/


@@html:<font color = "#4715b3">@@
‚á≤ We define $LD(n)$ as the least natural number /greater than $1$/
that divides $n$, or the /least divisor/ of $n$.
@@html:</font>@@

Every number has at least one divisor greater than /unit/ $1$.[fn:12]
Therefore, every number $n$ has a smallest, /least/ divisor, $LD(n)$,
in its set of divisors, even if that set has only one element. We will
leverage this fact to test a number for primeness. Read on.

Let's look at a /composite/ number $12$ with the set of divisors
$\{2,3,4,6,12\}$. Here, $LD(12) = 2$ and the *cardinality*[fn:13] of
the set is $5$, i.e., greater than $1$. What about $120$? The divisor
set[fn:14] would be much larger

\begin{align*}
\{2,3,4,5,6,8,10,12,15,20,24,30,40,60,120\}
\end{align*}

and we can get it with this Haskell code[fn:15]

#+name: 94268e73-4fb6-465b-b810-fdd34c488c72
#+begin_src haskell :eval never :exports code
divisors :: Integer -> [Integer]
divisors n = [d | d <- [2..abs n], abs n `mod` d == 0]
#+end_src

testing

#+name: 78636291-ee0d-4c8e-a94d-0dba38caad3d
#+begin_src haskell :results verbatim :exports both
divisors 120
#+end_src

#+RESULTS: 78636291-ee0d-4c8e-a94d-0dba38caad3d
: [2,3,4,5,6,8,10,12,15,20,24,30,40,60,120]

As with $12$, we see a pattern where we start with a divisor that is
prime, then (if any) composite numbers follow.[fn:16] In both examples
notice that the square the $LD(n)$, e.g., with $120$ squaring $2$ is
$4$, is less than $120$. Why is this germane?

Consider the composite number $143$. Its divisors are two prime
numbers, $11$ and $13$. Now, $11^2 = 121$, while $13^2 = 169$, i.e.,
the square of the former, i.e., the $LD(143)$, is less than
$143$. Neat observation, but does this help us in testing a number for
primeness? Yes, and we'll find out why below.

@@html:<font color = "#4715b3">@@
‚á≤ Proposition
1. If $n > 1$ then $LD(n)$ is a prime number.
2. If $n > 1$ and $n$ is not a prime number, then $(LD(n))^2 \le n$
@@html:</font>@@

The gist of these two implications will be turned into code that will
help test if a number is prime.

Why concentrate on the least divisor? The first part of our
proposition will anchor the fact that the least divisor is always a
prime. This will be necessary for when a number is analyzed for
primeness and no divisor less than the number itself has been
found. But we need to prove that least divisor is indeed prime. How?
We'll employ a proof method called /contradiction/[fn:17] where we
claim just the opposite of what we want to prove, then expose it as
not true. In this case, let's assume $c = LD(n)$ and $c$ is /not/
prime, a contradiction of our original statement. But if $c$ is not
prime it must then be a composite number itself, e.g., $c = a \cdot b$
where $1 \lt a \lt c$. But that would mean $a$ now divides $n$, and $a$ is
smaller than $c$ the supposed $LD(n)$. Therefore, a $LD(n)$ must be
prime.

Now, before attacking the second part, let's do an example.

@@html:<font color = "#0d3db3">@@
ùñüùï≠: Is $599$ a prime?
@@html:</font>@@

If $599$ is composite, then $599 = d \cdot e$ with $d,e \gt 1$. This implies

\begin{align*}
d &\le \sqrt{599} \quad or \\
e &\le \sqrt{599}
\end{align*}

because if both $d$ and $e$ were greater than $\sqrt{599}$ (the
opposite of less than or equal to) that would make $d \cdot e$ greater
than $\sqrt{599} \cdot \sqrt{599} = 599$, which it can't be. So if $599$
is composite, it has a divisor between $2$ and $\lfloor \sqrt{599} \rfloor$[fn:18]
or $24$.

At this point we can guess that to show $599$ is prime, we need to
show it has no divisor  not a composite number. $24^2 = 576$, the /floor/ of
$\sqrt{599}$, but $25^2 = 625$ which is overshoot. We can restate the
second part of the proposition thusly

@@html:<font color = "#4715b3">@@ ‚á≤ Proposition \\
Let $n$ be a natural number. If $n$ is /composite/ then $n$ has a
prime divisor between $2$ and $\lfloor\sqrt{n}\rfloor$. Thus, if $n \gt 1$ and $n$
has /no/ prime divisors between $2$ and $\lfloor\sqrt{n}\rfloor$, then $n$ must be
prime.  @@html:</font>@@

which allows us to quit looking for a $LD(n)$, i.e., some prime, long
before we get to $599$. If we test this algorithmic proposition on
$599$ we see that none of the primes between $2$ and $24$, namely the
set of primes $\{2,3,5,7,11,13,17,19,23\}$ divides $599$ evenly;
therefore, $599$ is prime.

We now have our main tool for establishing primality, namely, $LD(n)$
proven. Let's put it into code. We'll break it into two parts

#+name: 4d37a5ff-41e7-4efb-83ec-bdf53b60e009
#+begin_src haskell :eval never :exports code
ld :: Integral t => t -> t
ld n = ldf 2 n
ldf k n | divides k n = k
        | k^2 > n = n
        | otherwise = ldf (k+1) n
#+end_src

~ld~ is just a wrapper to feed our number in question into ~ldf~, the
"least divisor function" which will do the actual work. What we see
with ~ldf~ is Haskell's *equation guarding*, which allows us to set up
test *cases*. ~ldf~ first tries ~divides~ (which yields either ~True~
or ~False~) on the incoming number argument ~n~ using the argument
~k~. We see ~ldf~ begins with the lowest prime $2$ for ~k~. So if $2
\mid n$ then we're through, the $LD(n) = k$. But if it is not then we
move on to the next /case/, namely, Is ~k~ squared greater than
~n~?[fn:19] Again, this yields a ~True~ or ~False~. If ~True~, then
~n~ is the least divisor. But if this second test case also fails, we
go onto the third, which is not a normal test case, but a hardwired
~True~, i.e., we're going with this case---because there are no other
possible situations. What's unique about this last case is that it
simply calls itself, but with ~k~ incremented by $1$. In other words,
we have a repeating mechanism whereby we can march up through the
whole numbers one after the other testing each. This is *recursion*,
and we'll soon be exploring it in depth. But for now just accept that
~ldf~ is calling itself, driving inside itself, so to speak, to try
another time.

Let's investigate the number $5$ and how ~ldf~ handles it

#+name: 69a1dad9-38c5-471b-8ac5-7743b2380db9
#+begin_src haskell :results verbatim :exports both
ld 5
#+end_src

#+RESULTS: 69a1dad9-38c5-471b-8ac5-7743b2380db9
: 5

How did ~ld~ find this? In the first iteration, the first test case,
$2 \mid 5$, fails, followed by the second test case, ~2^2 > 5~, which
is also ~False~ since $4 \lt 5$. Hence, recursion ensues with ~ldf 3
5~. On the first test case ~divides 3 5~ also fails, but then the
second test case, ~3^2 > 5~ is ~True~; and so we know $5$ is indeed
the least divisor. And so we have turned our Proposition theorems into
an algorithm and working code! Let's see a more general
(non-abbreviated) explanation of /equation guarding/

#+name: 3f177117-e78a-4dbf-af1f-e58644006648
#+begin_src haskell :eval never :exports code
foo t | condition_1 = body_1
foo t | condition_2 = body_2
foo t | condition_3 = body_3
foo t |             = body_4
#+end_src

Now using our ~ld~ function, let's define our first version of a /test
for primes/

#+name: 6cf24bbe-c37f-4480-8b57-79509a9adc40
#+begin_src haskell :eval never :exports code
prime0 n | n < 1 = error "Not a positive integer"
         | n == 1 = False
         | otherwise = ld n == n
#+end_src

When we compile this, our Haskell REPL complains that we didn't
provide a type signature for ~prime0~ and then suggests one---or we
can simply ask

#+name: 9918a478-cb0f-4511-a63b-bae7c499fd9c
#+begin_src haskell :results verbatim :exports both
:t prime0
#+end_src

#+RESULTS: 9918a478-cb0f-4511-a63b-bae7c499fd9c
: prime0 :: Integral a => a -> Bool

Let's add it

#+name: 4d38f6dd-987b-4709-bee2-8de927d0ce94
#+begin_src haskell :eval never :exports code
prime0 :: Integral a => a -> Bool
prime0 n | n < 1 = error "Not a positive integer"
         | n == 1 = False
         | otherwise = ld n == n
#+end_src






#+name: c6de585a-8794-4046-8212-696711c686df
#+begin_src haskell :eval never :exports code
reduce ::  (Integral a) => a -> a -> Ratio a
{-# SPECIALISE reduce :: Integer -> Integer -> Rational #-}
reduce _ 0              =  ratioZeroDenominatorError
reduce x y              =  (x `quot` d) :% (y `quot` d)
                           where d = gcd x y

x % y                   =  reduce (x * signum y) (abs y)

numerator   (x :% _)    =  x
denominator (_ :% y)    =  y
#+end_src


#+name: a8533852-2631-4895-9d14-54c6360a9fa8
#+begin_src haskell :eval never :exports code
gcd             :: (Integral a) => a -> a -> a
gcd x y         =  gcd' (abs x) (abs y)
                   where gcd' a 0  =  a
                         gcd' a b  =  gcd' b (a `rem` b)
#+end_src

#+name: 8cf8add5-a62e-44d4-95d8-5836ecf08cf7
#+begin_src haskell :eval never :exports code
lcm             :: (Integral a) => a -> a -> a
lcm _ 0         =  0
lcm 0 _         =  0
lcm x y         =  abs ((x `quot` (gcd x y)) * y)
#+end_src


‚•§ *Exclusive or*

An *exclusive or* is the negation of an implication

#+NAME: XORTAB1
#+ATTR_HTML: :border 2 :rules all :frame border
| <c> | <c> |          <c>           |    <c>     |
|  P  |  Q  | P $\nleftrightarrow Q$ | P $\iff Q$ |
|-----+-----+------------------------+------------|
|  t  |  t  |           f            |     t      |
|  t  |  f  |           t            |     f      |
|  f  |  t  |           t            |     f      |
|  f  |  f  |           f            |     t      |
|-----+-----+------------------------+------------|
\\

This shows that *exclusive or* is indeed the negation of *if and only
if equivalence*. Only if the arguments $P$ and $Q$ have different
truth values is the *exclusive or* true.

For example, /Did $A$ (exclusive) or $B$ win?/ So "win" as in true;
hence, both cannot have won, nor can both have lost.




#+INCLUDE: "./footer.org" :minlevel 1

* Footnotes

[fn:1] We'll discuss another possibility, namely, the Platonic
belief math is just out there in the universe, later.

[fn:2] Let's watch [[https://youtu.be/8xPRn9weLYg?si=5fplvY_vDQD2dbHb][this video]] (semi-)realistically depicting a STEM
environment. No magic, just people taking math out of books and
putting it into their technology.

[fn:3] A fraction can be resolved into a *[[https://en.wikipedia.org/wiki/Fraction#Decimal_fractions_and_percentages][decimal fraction]]*, e.g.,
$\frac{3}{4}$ is $0.75$. But even repeating decimal, e.g.,
$\frac{1}{3} = 0.333\ldots$ where the number to the right of the
decimal is an *infinite series*, i.e., $\frac{3}{10} + \frac{3}{100} +
\frac{3}{1000} \ldots$.

[fn:4] In higher math the *natural counting numbers*
$\{1,2,3,4,\ldots\}$ are signified by $\mathbb{N}$, while the
*integers* $\{\ldots,-2,-1,0,1,2,\ldots\}$ are $\mathbb{Z}$. Both are
whole numbers.

[fn:5] Fundamental to Peano's approach is recursion and induction,
which we will explore in depth soon.

[fn:6] This is not serious number factoring code. The "art" of
factoring numbers is at the heart of modern cryptography. In fact, it
is still unknown whether any variation of factoring code running on
conventional computers can factor any significantly large number in a
reasonable amount of time. This issue belongs to the realm of
*[[https://en.wikipedia.org/wiki/Computability_theory][computability theory]]* and was the instigator to the digital computer.

[fn:7] In Haskell, everything has a value. This is an abstraction
meant to keep things highly mathematical, i.e., above imperative
programming where evaluating code leads to state changes. With
Haskell, the evaluation of code yields values intentionally not
involved with state changes.

[fn:8] The [[https://en.wikipedia.org/wiki/Pythagorean_trigonometric_identity][proof/derivation]] of $sin^{2}\theta + cos^{2}\theta = 1$ is usually included
in trigonometry texts. This is just one proven identity. But the
general idea of *[[https://en.wikipedia.org/wiki/Rewriting][term rewriting]]*, i.e., the art of redoing an
expression into an identical one, is an important field in math and
computability.

[fn:9] ~:t~ and ~:i~ are your best friends when navigating the Haskell
type world.

[fn:10] In mathematical logic statements, expressions $p$ and $q$ are
*[[https://en.wikipedia.org/wiki/Logical_equivalence][logically equivalent]]* if they have the same /truth value/ in every
case of possible inputs for $p$ and $q$. Moral of the story: Keep
/is-defined-as/ ~=~ separate from /equivalence/ ~==~ (or
non-equivalence ~/+~).

[fn:11] Technically only numbers can be added, subtracted,
etc. For addition, e.g., non-number objects are /aggregated/.

[fn:12] Again, a prime number has exactly one divisor greater than $1$, i.e.,
the prime number itself.

[fn:13] The *cardinality* of a set is the count of the unique elements
contained in the set.

[fn:14] ...or the /factors/ of $120$.

[fn:15] This is the Haskell version of *[[https://en.wikipedia.org/wiki/Set-builder_notation][set comprehension]]*. More
later.

[fn:16] Let's not forget the /Fundamental Theorem of Arithmetic/,
i.e., that $120$ can also be expressed as product of powers of those
first three primes.

[fn:17] See [[https://en.wikipedia.org/wiki/Proof_by_contradiction][proof by contradiction]].

[fn:18] The *floor* ($\lfloor\; \rfloor$) of $\sqrt{599}$? First, get the square
root of $599$, which is $24.474476501040833$; so the /lowest/ whole
number, the floor, would be $24$. The $\lceil\sqrt{599}\rceil$, or the *ceiling*
of the square root of $599$ would be $25$.

[fn:19] This is the second part of $LD(n)$ Proposition, i.e., we've
flipped the $(LD(n))^2 \le n$. ~ld~ looks for a prime divisor of $n$ by
checking $k \mid n$ for all $k$ with $2 \le k \le \sqrt{n}$. But isn't a
$LD(n)$ supposed to be prime? We will improve on ~ld~ so it doesn't
waste time checking non-prime $k$ within $2 \le k \le \sqrt{n}$.

[fn:20] DEFINITION NOT FOUND.
